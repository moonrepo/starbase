COMMENT    = _{ "#" ~ (!"#" ~ ANY)* }
WHITESPACE = _{ " " | "\t" }

id     = _{ ASCII_ALPHANUMERIC | "_" }
id_env = _{ ASCII_ALPHA_UPPER | ASCII_DIGIT | "_" }

// SYNTAX:
// https://www.gnu.org/software/bash/manual/html_node/Definitions.html

blank       = _{ " " | "\t" }
whitespace  = _{ blank | "\n" | "\r" }
meta_char   = _{ whitespace | "|" | "&" | ";" }
escape_char = _{ "\\" | "/" | "b" | "f" | "n" | "r" | "t" }
fd_char     = _{ ASCII_DIGIT | "-" }

// OPERATORS:

control_operator          =  {
    // Posix
    ";"
  | "&&"
  | "||"
  | "--"
}
redirect_operator         =  {
    // Posix
    "<>"
  | ">>>"
  | ">>"
  | "<<<"
  | "<<"
  | "&>>"
  | "&>"
  | ">&"
  | "<&"
  | ">|"
  | ">"
  | "<"
}
redirect_operator_with_fd = @{
    (fd_char ~ redirect_operator ~ fd_char)
  | (fd_char ~ redirect_operator)
  | (redirect_operator ~ fd_char)
}

operator = _{
    control_operator
  | redirect_operator_with_fd
  | redirect_operator
}

// VALUES:

value_double_quote_inner = _{
    !("\"" | "\\") ~ ANY
  | "\\" ~ ("\"" | escape_char)
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
value_double_quote       = @{ "\"" ~ value_double_quote_inner* ~ "\"" }

value_single_quote_inner = _{
    !("'" | "\\") ~ ANY
  | "\\" ~ ("'" | escape_char)
  | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
value_single_quote       = @{ "'" ~ value_single_quote_inner* ~ "'" }

value_ansi_quote = @{ "$'" ~ value_single_quote_inner* ~ "'" }

value_unquoted_inner = _{ !(meta_char | operator) ~ ANY }
value_unquoted       = @{ value_unquoted_inner+ }

value = _{ value_ansi_quote | value_double_quote | value_single_quote | value_unquoted }

// EXPANSIONS:
// https://www.gnu.org/software/bash/manual/html_node/Arithmetic-Expansion.html

arithmetic_expansion = { "$((" ~ (!"))" ~ ANY)* ~ "))" }
parameter_expansion  = { "${" ~ (!"}" ~ ANY)* ~ "}" }

expansion = _{ arithmetic_expansion | parameter_expansion }

// ARGUMENTS:

env_var_namespace =  { ^"$e:" | ^"$env::" | ^"$env:" | ^"$env." }
env_var_name      = @{ id_env+ }
env_var           = ${ env_var_namespace? ~ env_var_name ~ "=" ~ value }

flag_group = @{ "-" ~ ASCII_ALPHA{2, } }
flag       = @{ "-" ~ ASCII_ALPHA }

option            = @{ "--" ~ (id | "-" | ".")+ }
option_with_value = ${ option ~ "=" ~ value }

param = @{ "$" ~ (ASCII_ALPHA | "_") ~ id* }

argument = _{ expansion | env_var | param | flag_group | flag | option_with_value | option | value }

// SUBSTITUTION:

command_substitution = { "${" ~ (whitespace | "|") ~ pipeline ~ "; }" | "$(" ~ pipeline ~ ")" | "`" ~ pipeline ~ "`" }
process_substitution = { "<(" ~ pipeline ~ ")" | ">(" ~ pipeline ~ ")" }

substitution = _{ command_substitution | process_substitution }

// COMMAND LINE:
// https://www.gnu.org/software/bash/manual/html_node/Shell-Commands.html

command            = { argument+ }
command_terminator = { ";" | "&" | "2>&1" | "\n" }
command_list       = { command ~ ((process_substitution) | (operator ~ command))* ~ command_terminator? }

pipeline_negated  = { "!" }
pipeline_operator = { "|" | "|&" }
pipeline          = { pipeline_negated? ~ command_list ~ (pipeline_operator ~ command_list)* }

args = _{
    SOI ~ pipeline ~ EOI
}
